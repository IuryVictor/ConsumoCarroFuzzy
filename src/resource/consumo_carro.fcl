FUNCTION_BLOCK consumo_carro
    VAR_INPUT
        potencia : REAL;
        peso : REAL;
        aceleracao : REAL;
    END_VAR

    VAR_OUTPUT
        consumo : REAL;
    END_VAR

    FUZZIFY potencia
        TERM pouco_potente := (40, 1) (100, 1) (120, 0);
        TERM medio := (100, 0) (120, 1) (160, 1) (180, 0);
        TERM potente := (160, 0) (180, 1) (220, 1);
    END_FUZZIFY

    FUZZIFY peso
        TERM leve := (1500, 1) (2500, 1) (3000, 0);
        TERM medio := (2500, 0) (3000, 1) (3500, 1) (4000, 0);
        TERM pesado := (3500, 0) (4000, 1) (5000, 1);
    END_FUZZIFY

    FUZZIFY aceleracao
        TERM lento := (8, 1) (12, 1) (15, 0);
        TERM medio := (12, 0) (15, 1) (18, 1) (21, 0);
        TERM rapido := (19, 0) (21, 1) (24, 1);
    END_FUZZIFY

    DEFUZZIFY consumo
        TERM economico := (10, 1) (20, 1) (25, 0);
        TERM medio := (20, 0) (25, 1) (30, 1) (35, 0);
        TERM nao_economico := (30, 0) (35, 1) (45, 1);
        METHOD : COG; // metodo centro de gravidade
        DEFAULT := 0; // valor 0 caso não ative nenhuma regra
    END_DEFUZZIFY

    RULEBLOCK No1
        AND : MIN; // AND minimo OR maximo
        ACT : MIN; // ativação minima
        ACCU : MAX; //acumulação maxima

        RULE 1 : IF potencia IS potente AND peso IS pesado AND aceleracao IS lento THEN consumo IS nao_economico;
        RULE 2 : IF potencia IS pouco_potente AND peso IS leve AND aceleracao IS rapido THEN consumo IS economico;
        RULE 3 : IF potencia IS pouco_potente AND peso IS pesado AND aceleracao IS medio THEN consumo IS medio;
        RULE 4 : IF potencia IS potente AND peso IS medio AND aceleracao IS lento THEN consumo IS medio;
        RULE 5 : IF potencia IS potente AND peso IS medio AND aceleracao IS medio THEN consumo IS medio;
    END_RULEBLOCK
END_FUNCTION_BLOCK

